;ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
;³ lumberjacks                                       ³
;ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
;³                                                   ³
;³ copyright (C) 2017 jeff panici                    ³
;³ https://nybbles.io                                ³
;³                                                   ³
;³                                                   ³
;³                                                   ³
;ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

;ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
;³ constants                                         ³
;ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
CRT_IC      equ 03d4h
CRT_IM      equ 03b4h
ATT_IW      equ 03c0h
GRA_I       equ 03ceh
SEQ_I       equ 03c4h
PEL_IW      equ 03c8h
PEL_IR      equ 03c7h

CRT_DC      equ 03d5h
CRT_DM      equ 03b5h
ATT_R       equ 03c1h
GRA_D       equ 03cfh
SEQ_D       equ 03c5h
MIS_R       equ 03cch
MIS_W       equ 03c2h
IS1_RC      equ 03dah
IS1_RM      equ 03bah
PEL_D       equ 03c9h
PEL_MSK     equ 03c6h

VRAM        equ 0a000h
SZ_VRAM     equ 0ffffh
VRAM_PITCH  equ 256

;ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
;³ structures                                        ³
;ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
vga_reg_cfg struc [bp]
    vga_reg     dw ?
    vga_func    db ?
    vga_value   db ?
    vga_flags   db ?
    vga_pad     db ?
ends

;ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
;³ variables                                         ³
;ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
even

modeq_params:
    ; misc output
    dw  032ch
    db  00h,  0e3h,  0, 0

    ; horizonal total
    dw  03d4h
    db  00h,  05fh,  0, 0

    ; horizontal display enable end
    dw  03d4h
    db  01h,  03fh,  0, 0

    ; horizontal blank start
    dw  03d4h
    db  02h,  040h,  0, 0

    ; horizontal blank end
    dw  03d4h
    db  03h,  082h,  0, 0

    ; horizontal retrace start
    dw  03d4h
    db  04h,  04ah,  0, 0

    ; horizontal retrace end
    dw  03d4h
    db  05h,  09ah,  0, 0

    ; vertical total
    dw  03d4h
    db  06h,  023h,  0, 0

    ; overflow register
    dw  03d4h
    db  07h,  0b2h,  0, 0

    ; preset row scan
    dw  03d4h
    db  08h,     0,  0, 0

    ; max scan line/char horizontal time
    dw  03d4h
    db  09h,  061h,  0, 0

    ; vertical retrace start
    dw  03d4h
    db  010h,  0ah,  0, 0

    ; vertical retrace end
    dw  03d4h
    db  011h, 0ach,  0, 0

    ; vertical display enable end
    dw  03d4h
    db  012h, 0ffh,  0, 0

    ; offset/logical width
    dw  03d4h
    db  013h, 020h,  0, 0

    ; underline location
    dw  03d4h
    db  014h, 040h,  0, 0

    ; vertical blank start
    dw  03d4h
    db  015h,  07h,  0, 0

    ; vertical blank end
    dw  03d4h
    db  016h, 01ah,  0, 0

    ; mode control
    dw  03d4h
    db  017h, 0a3h,  0, 0

    ; clock mode register
    dw  03c4h
    db  01h,     1,  0, 0

    ; memory mode register
    dw  03c4h
    db  04h,   0eh,  0, 0

    ; mode register
    dw  03ceh
    db  05h,  040h,  0, 0

    ; misc register
    dw  03ceh
    db  06h,   05h,  0, 0

    ; mode control
    dw  03c0h
    db  010h, 041h, 32, 0

    ; horizontal panning
    dw  03c0h
    db  013h,    0, 32, 0

    ; end marker
    dw  0
    db  0,       0,  0, 0

;ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
;³ sc_on                                             ³
;ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
sc_on macro
    push    ax, dx
    inp     IS1_RC
    outp    ATT_IW, 020h
    pop     dx, ax
#em

;ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
;³ sc_off                                            ³
;ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
sc_off macro
    push    ax, dx
    inp     IS1_RC
    outp    ATT_IW, 0
    pop     dx, ax
#em

;ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
;³ _set_pal_idx                                      ³
;ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
_set_pal_idx:
    mov     bp, sp
    mov     ax, [bp + 2]
    mov     dx, PEL_IW
    out     dx, al
    ret

sc_pidx macro
    push    bp, ax, dx
    push    #1
    call    _set_pal_idx
    add     sp, 2
    pop     dx, ax, bp
#em

;ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
;³ _set_pal_color                                    ³
;ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
_set_pal_color:
    struc [bp]
                dw ?
        pal_red db ?
        pal_grn db ?
        pal_blu db ?
        pal_pad db ?
    ends

    mov     bp, sp
    mov     dx, PEL_D
    mov     al, pal_red
    out     dx, al
    mov     al, pal_grn
    out     dx, al
    mov     al, pal_blu
    out     dx, al
    ret

sc_prgb macro
    push    bp, ax, dx
    push    #3 by #2, #1 by 0
    call    _set_pal_color
    add     sp, 4
    pop     dx, ax, bp
#em

;ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
;³ _is_vblank                                        ³
;ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
_is_vblank:
    mov     dx, IS1_RC
    in      al, dx
    and     al, 00001000xb
    ret

sc_isvb macro
    push    dx
    call    _is_vblank
    pop     dx
#em

;ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
;³ _wait_vblank                                      ³
;ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
_wait_vblank:
l0: mov     dx, IS1_RC
    in      al, dx
    and     al, 00001000xb
    jz      l0
    ret

sc_wvb macro
    push    dx, ax
    call    _wait_vblank
    pop     ax, dx
#em

;ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
;³ _wait_novblank                                    ³
;ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
_wait_novblank:
l0: mov     dx, IS1_RC
    in      al, dx
    and     al, 00001000xb
    jnz     l0
    ret

sc_wnvb macro
    push    dx, ax
    call    _wait_novblank
    pop     ax, dx
#em

;ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
;³ sc_mo13                                           ³
;ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
sc_mo13 macro
    mov     ah, 0
    mov     al, 13h
    int     10h
    call    font_init
#em

;ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
;³ sc_mo03                                           ³
;ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
sc_mo03 macro
    mov     ah, 0
    mov     al, 3
    int     10h
#em

;ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
;³ _mode_prg                                         ³
;ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
_mode_prg:
    mov     dx, CRT_IC
    mov     al, 011h
    out     dx, al
    inc     dx
    in      al, dx
    and     al, 07fh
    mov     ah, al
    mov     al, 011h
    dec     dx
    out     dx, ax
l2: mov     dx, vga_reg
    cmp     dx, 0
    je      >l0
    mov     al, vga_func
    mov     bl, vga_flags
    cmp     bl, 0
    jz      >l1
    add     al, bl
l1: out     dx, al
    inc     dx
    mov     al, vga_value
    out     dx, al
    add     bp, type vga_reg_cfg
    jmp     l2
l0: mov     dx, CRT_IC
    mov     al, 011h
    out     dx, al
    inc     dx
    in      al, dx
    or      al, 080h
    mov     ah, al
    mov     al, 011h
    dec     dx
    out     dx, ax
    ret

sc_mprg macro
    push    bp, ax, bx, dx
    mov     bp, #1
    call    _mode_prg
    pop     dx, bx, ax, bp
#em
