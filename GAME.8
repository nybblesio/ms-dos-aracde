;зддддддддддддддддддддддддддддддддддддддддддддддддддд©
;Ё lumberjacks                                       Ё
;цддддддддддддддддддддддддддддддддддддддддддддддддддд╢
;Ё                                                   Ё
;Ё copyright (C) 2017 jeff panici                    Ё
;Ё https://nybbles.io                                Ё
;Ё                                                   Ё
;Ё                                                   Ё
;Ё                                                   Ё
;юддддддддддддддддддддддддддддддддддддддддддддддддддды

code segment
    jmp     start

include constant.8
include macro.8
include string.8
include file.8
include control.8
include memory.8
include io.8
include input.8
include bank.8
include timer.8
include vga.8
include video.8
include sound.8

;зддддддддддддддддддддддддддддддддддддддддддддддддддд©
;Ё variables                                         Ё
;юддддддддддддддддддддддддддддддддддддддддддддддддддды
even

state               db      ?
fps                 db      ?
exit_code           db      ?
temp_ptr            dw      ?
draw_flag           db      ?

frame_rate:         s$_def  '00'
game_title:         s$_def  'LUMBERJACKS'
game_copyright:     s$_def  '(C) 2017 NYBBLES.IO'

input_events:
quit_key_evt:       in_evt  T_INPUT_KEY, K_ESC

p1_left_key_evt:    in_evt  T_INPUT_KEY, K_LEFT
p1_left_joy_evt:    in_evt  T_INPUT_JOY, 1

p1_right_key_evt:   in_evt  T_INPUT_KEY, K_RIGHT
p1_right_joy_evt:   in_evt  T_INPUT_JOY, 1

p1_up_key_evt:      in_evt  T_INPUT_KEY, K_UP
p1_up_joy_evt:      in_evt  T_INPUT_JOY, 1

p1_down_key_evt:    in_evt  T_INPUT_KEY, K_DOWN
p1_down_joy_evt:    in_evt  T_INPUT_JOY, 1

                    in_evt  0,           0

input_bindings:
quit_bind:          in_bind B_QUIT,     'QUIT            ', exit,      quit_key_evt,     0,                0, 0
p1_left_bind:       in_bind B_P1_LEFT,  'P1 LEFT         ', _p1_left,  p1_left_key_evt,  p1_left_joy_evt,  0, 0
p1_right_bind:      in_bind B_P1_RIGHT, 'P1 RIGHT        ', _p1_right, p1_right_key_evt, p1_right_joy_evt, 0, 0
p1_up_bind:         in_bind B_P1_UP,    'P1 UP           ', _p1_up,    p1_up_key_evt,    p1_up_joy_evt,    0, 0
p1_down_bind:       in_bind B_P1_DOWN,  'P1 DOWN         ', _p1_down,  p1_down_key_evt,  p1_down_joy_evt,  0, 0

                    in_bind 0,          '                ', 0,         0,                0,                0, 0

;зддддддддддддддддддддддддддддддддддддддддддддддддддд©
;Ё _p1_right                                         Ё
;юддддддддддддддддддддддддддддддддддддддддддддддддддды
_p1_right:
    vg_lspr 0
    es add  s_x, 4
    es cmp  s_x, 230
    jb      >l0
    es mov  s_x, 230
l0: ret

;зддддддддддддддддддддддддддддддддддддддддддддддддддд©
;Ё _p1_down                                          Ё
;юддддддддддддддддддддддддддддддддддддддддддддддддддды
_p1_down:
    vg_lspr 0
    es add  s_y, 4
    es cmp  s_y, 230
    jb      >l0
    es mov  s_y, 230
l0: ret

;зддддддддддддддддддддддддддддддддддддддддддддддддддд©
;Ё _p1_left                                          Ё
;юддддддддддддддддддддддддддддддддддддддддддддддддддды
_p1_left:
    vg_lspr 0
    es sub  s_x, 4
    es cmp  s_x, 10
    ja      >l0
    es mov  s_x, 10
l0: ret

;зддддддддддддддддддддддддддддддддддддддддддддддддддд©
;Ё _p1_up                                            Ё
;юддддддддддддддддддддддддддддддддддддддддддддддддддды
_p1_up:
    vg_lspr 0
    es sub  s_y, 4
    es cmp  s_y, 10
    ja      >l0
    es mov  s_y, 10
l0: ret

;зддддддддддддддддддддддддддддддддддддддддддддддддддд©
;Ё _fps_timer                                        Ё
;юддддддддддддддддддддддддддддддддддддддддддддддддддды
fps_timer:
    tm_clr
    mov     di, frame_rate + 1
    xor     ax, ax
    cs mov  al, fps
    call    s$_dec2
    cs mov  fps, 0
    ret

;зддддддддддддддддддддддддддддддддддддддддддддддддддд©
;Ё allocate                                          Ё
;юддддддддддддддддддддддддддддддддддддддддддддддддддды
allocate:
    mm_resv SZ_CTRL_RAM / 16,  offset ctrl_ram_ptr
    mm_set  ctrl_ram_ptr, 0, SZ_CTRL_RAM, 0

    mov     es, ctrl_ram_ptr
    mov     bp, 0

    mm_resv SZ_TILE_BANK / 16, offset temp_ptr
    mm_set  temp_ptr, 0, SZ_TILE_BANK, 024h
    es_movm c_bg_tiles, temp_ptr

    mm_resv SZ_TILE_BANK / 16, offset temp_ptr
    mm_set  temp_ptr,   0, SZ_TILE_BANK, 07fh
    mm_set  temp_ptr,   0, 128, 07ah
    mm_set  temp_ptr, 128, 128, 08ah
    mm_set  temp_ptr, 256, 256, 09ah
    mm_set  temp_ptr, 512, 256, 0abh
    mm_set  temp_ptr, 768, 256, 0bah
    es_movm c_spr_tiles, temp_ptr

    mm_resv SZ_VRAM / 16, offset back_buffer_ptr
    mm_set  back_buffer_ptr, 0, SZ_VRAM, 0

    ret

;зддддддддддддддддддддддддддддддддддддддддддддддддддд©
;Ё init                                              Ё
;юддддддддддддддддддддддддддддддддддддддддддддддддддды
init:
    mm_init
    call    allocate
    call    vg_init
    call    io_init
    call    tm_init
    sn_init

    vg_lspr 0
    vg_sspr 0, 0, 100, 10, F_SPR_ENABLED, 0

    tm_new  offset fps_timer, 60

    ret

;зддддддддддддддддддддддддддддддддддддддддддддддддддд©
;Ё draw_title                                        Ё
;юддддддддддддддддддддддддддддддддддддддддддддддддддды
draw_title:
    vg_fbox 50 by 33, 150 by 85, 8 by 0
    vg_box  50 by 33, 150 by 85, 0 by 0

    vg_str  offset large_font, game_title, 80 by 45, 1 by 0, 0
    vg_str  offset large_font, game_title, 81 by 46, 7 by 0, 0

    vg_str  offset micro_font, game_title, 90 by 74, 1 by 0, 1
    vg_str  offset micro_font, game_title, 91 by 75, 7 by 0, 1

    vg_str  offset micro_font, game_copyright, 62 by 90, 1 by 0, 1
    vg_str  offset micro_font, game_copyright, 63 by 91, 7 by 0, 1

    ret

;зддддддддддддддддддддддддддддддддддддддддддддддддддд©
;Ё draw                                              Ё
;юддддддддддддддддддддддддддддддддддддддддддддддддддды
draw:
    cs cmp  draw_flag, 1
    je      >l0
    ret
l0: cs mov  draw_flag, 0
    vg_bg1
    vg_fg
    vg_bg2
    call    draw_title
#if DEBUG
    vg_str  offset micro_font, frame_rate, 244 by 251, 0 by 0, 1
#endif
    ret

;зддддддддддддддддддддддддддддддддддддддддддддддддддд©
;Ё show                                              Ё
;юддддддддддддддддддддддддддддддддддддддддддддддддддды
show:
    sc_isvb
    cmp     al, 0
    jnz     >l0
    ret
l0: vg_flip
    sc_wnvb
    in_upd  input_events
    in_fire input_bindings
    cs inc  fps
    cs mov  draw_flag, 1
    ret

;зддддддддддддддддддддддддддддддддддддддддддддддддддд©
;Ё start                                             Ё
;юддддддддддддддддддддддддддддддддддддддддддддддддддды
start:
    call    init

;зддддддддддддддддддддддддддддддддддддддддддддддддддд©
;Ё game_loop                                         Ё
;юддддддддддддддддддддддддддддддддддддддддддддддддддды
game_loop:
    tm_fire
    cs call draw
    cs call show
    jmp     game_loop

;зддддддддддддддддддддддддддддддддддддддддддддддддддд©
;Ё exit                                              Ё
;юддддддддддддддддддддддддддддддддддддддддддддддддддды
exit:
    sn_shut
    call    tm_shutdown
    call    io_shutdown
    call    vg_shutdown
    mov     ah, 04ch
    mov     al, exit_code
    int     21h
    ret
