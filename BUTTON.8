;ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
;³ banked                                            ³
;ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
;³                                                   ³
;³ copyright (C) 2017 jeff panici                    ³
;³ https://nybbles.io                                ³
;³                                                   ³
;³                                                   ³
;³                                                   ³
;ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

;ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
;³ constants                                         ³
;ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
F_BTN_NONE      equ 0000_0000_0000_0000xb
F_BTN_VISIBLE   equ 0000_0000_0000_0001xb
F_BTN_ENABLED   equ 0000_0000_0000_0010xb
F_BTN_NODRAW    equ 0000_0000_0000_0100xb
F_BTN_TXTONLY   equ 0000_0000_0000_1000xb
F_BTN_DEBOUNCE  equ 0000_0000_0001_0000xb
F_BTN_END       equ 1000_0000_0000_0000xb

;ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
;³ structures                                        ³
;ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
button struc [bp]
    btn_flags   dw ?
    btn_txt     dw ?
    btn_txt_pos dw ?
    btn_pos     dw ?
    btn_size    dw ?
    btn_bg_clr  dw ?
    btn_bdr_clr dw ?
    btn_func    dw ?
ends

;ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
;³ macros                                            ³
;ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
bt_def macro
    dw #1, #2, #3, #4, #5, #6, #7, #8
#em

bt_set macro
    push    bp
    mov     bp, #1
    mov     btn_flags, #2
    pop     bp
#em

;ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
;³ _btn_reset                                        ³
;ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
_btn_reset:
    mov     bp, sp
    mov     bp, [bp + 4]
l0: mov     ax, btn_flags
    test    ax, F_BTN_END
    jz      >l1
    ret
l1: test    ax, F_BTN_DEBOUNCE
    jz      >l2
    and     ax, 1111_1111_1110_1111xb
    mov     btn_flags, ax
l2: add     bp, type button
    jmp     l0
    ret

;ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
;³ _btn_fire                                         ³
;ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
_btn_fire:
    mov     bp, sp
    mov     bp, [bp + 4]
    mov     bx, ms_pos
    mov     ax, ms_btn
    test    al, F_LEFT_MOUSE_BTN
    jnz     >l0
    call    _btn_reset
    ret
l0: mov     bp, sp
    mov     bp, [bp + 2]
l1: mov     ax, btn_flags
    test    ax, F_BTN_END
    jz      >l2
    ret
l2: test    ax, F_BTN_DEBOUNCE
    jnz     >l4
    test    ax, F_BTN_ENABLED
    jz      >l4
l3: mov     ax, btn_pos
    mov     cx, btn_size
    mov     dx, ax
    add     dx, cx
    cmp     bh, al
    jb      >l4
    cmp     bh, dl
    ja      >l4
    cmp     bl, ah
    jb      >l4
    cmp     bl, dh
    ja      >l4
    cmp     btn_func, 0
    jz      >l4
    mov     ax, btn_flags
    or      ax, F_BTN_DEBOUNCE
    mov     btn_flags, ax
    call    [btn_func]
    ret
l4: add     bp, type button
    jmp     l1
    ret

bt_fire macro
    push    bp, ax, bx, cx, dx
    push    #1, #2
    call    _btn_fire
    add     sp, 4
    pop     dx, cx, bx, ax, bp
#em

;ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
;³ _btn_draw                                         ³
;ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
_btn_draw:
    mov     ax, btn_flags
    test    ax, F_BTN_ENABLED
    setnz   cl
    test    ax, F_BTN_TXTONLY
    setnz   ch
    cmp     ch, 1
    jz      >l0
    push    cx
    mov     bx, btn_pos
    mov     cx, btn_size
    mov     dx, btn_bg_clr
    vg_fbox bx, cx, dx
    mov     dx, btn_bdr_clr
    vg_box  bx, cx, dx
    pop     cx
l0: mov     dx, 8 by 0
    cmp     cl, 1
    jnz     >l1
    mov     dx, 7 by 0
l1: mov     si, btn_txt
    mov     bx, btn_txt_pos
    vg_str  micro_font, si, bx, dx, 1
    ret

;ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
;³ _btns_draw                                        ³
;ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
_btns_draw:
l0: cs mov  ax, btn_flags
    test    ax, F_BTN_NODRAW
    jnz     >l3
l1: test    ax, F_BTN_END
    jz      >l2
    ret
l2: test    ax, F_BTN_VISIBLE
    jz      >l3
    call    _btn_draw
l3: add     bp, type button
    jmp     l0
    ret

bt_draw macro
    push    bp, ax, bx, cx, dx, si
    mov     bp, #1
    call    _btns_draw
    pop     si, dx, cx, bx, ax, bp
#em
